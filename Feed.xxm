[[@xxmSession,DataLank,fCommon,Variants]][[!var
id,rw:integer;
c,cat,lbl:string;
ur:Variant;
db:TDataConnection;
]][[

id:=Context['id'].AsInteger;
case Context['x'].AsInteger of
  1://update
   begin
    db:=Session.Connection;
    db.BeginTrans;
    try
      lbl:=Context['label'].Value;
      cat:=Context['category'].Value;
      if cat='---' then cat:=Context['categoryNew'].Value;
      c:=CheckColor(Context['color'].Value);
      rw:=Context['readwidth'].AsInteger;
      if Context['autounread0'].Value='' then
        ur:=Null
      else
        case Context['autounread1'].AsInteger of
          0:ur:=Context['autounread0'].AsInteger;//hours
          1:ur:=Context['autounread0'].AsInteger*24;//days
          2:ur:=Context['autounread0'].AsInteger*24*7;//weeks
        end;
      db.Execute('update "Subscription" set'+
        ' category=?,label=?,color=?,readwidth=?,autounread=?'+
        ' where id=? and user_id=?',[cat,lbl,c,rw,ur,id,Session.UserID]);
      db.CommitTrans;
    except
      db.RollbackTrans;
      raise;
    end;
    Context.Redirect('Feeds.xxm',true);
   end;
  2://remove
   begin
    db:=Session.Connection;
    db.BeginTrans;
    try
      db.Execute('delete from "UserPost" where id in'+
        ' (select X.id from "UserPost" X'+
        ' inner join "Post" P on P.id=X.post_id'+
        ' inner join "Subscription" S on S.feed_id=P.feed_id and S.user_id=X.user_id'+
        ' where X.user_id=? and P.feed_id=?)',[Session.UserID,id]);
      db.Execute('delete from "SubCount" where subscription_id in '+
        '(select id from "Subscription" where id=? and user_id=?)',[id,Session.UserID]);
      db.Execute('delete from "Subscription" where id=? and user_id=?',[id,Session.UserID]);
      db.CommitTrans;
    except
      db.RollbackTrans;
      raise;
    end;
    Context.Redirect('Feeds.xxm',true);
   end
  else
    raise Exception.Create('Unknown action');
end;